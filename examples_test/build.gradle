plugins {
    id 'com.android.application'
}

Properties local_properties = new Properties()
local_properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.six15.examples_test"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "SCANDIT_LICENSE_KEY", "\"" + local_properties.getProperty("SCANDIT_LICENSE_KEY") + "\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            enableV3Signing(true)
            enableV4Signing(true)
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = rootProject.file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "examples_test_" + variant.versionName + ".apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    api "com.scandit.datacapture:core:6.16.0"
    api "com.scandit.datacapture:barcode:6.16.0"

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation project(':examples')
    implementation project(':hudservice-aidl')
    implementation project(':vosk-speech-recognition')
}